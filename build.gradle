import net.fabricmc.loom.task.RemapJar

import java.nio.charset.StandardCharsets
import java.time.Year

plugins {
    id "fabric-loom" version "0.2.0-SNAPSHOT"
    id "net.minecrell.licenser" version "0.2.1"
    id "com.matthewprenger.cursegradle" version "1.1.2"
    id "maven-publish"
    //id 'com.github.johnrengelman.shadow' version '4.0.4'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

group = "com.github.GlassPane"
archivesBaseName = "Mesh"
version = "${mod_version}"

minecraft {
}

processResources {
    // this will ensure that this task is redone when there"s a change
    inputs.property "version", project.version

    // replace stuff in fabric.mod.json, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"

        // add mod metadata
        expand "version": project.version
    }

    // copy everything else, that"s not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

repositories {
    mavenCentral()
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven"
    }
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
}

//configurations {
//    embed {
//        transitive = false
//    }
//}

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}"
    modCompile "net.fabricmc:fabric-loader:${loader_version}"

    compileOnly "com.google.code.findbugs:jsr305:${findbugs_version}"
}

compileJava {
    options.encoding = "UTF-8"
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption("Xdoclint:none", "-quiet")
        }
    }
}

jar {
    from "LICENSE.md"
    manifest.mainAttributes(
            "Implementation-Title": project.archivesBaseName,
            "Implementation-Version": project.version,
            "Maven-Artifact": "${project.group}:${project.name}:${project.version}".toLowerCase(Locale.ROOT),
            "Built-On-Minecraft": "${mc_version}",
            "Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
    )
}

task apiJar(type: Jar, dependsOn: jar) {
    onlyIf {
        project.hasProperty("api_package")
    }
    from (sourceSets.main.allJava) {
        include String.valueOf(findProperty("api_package")).replace('.', '/') + "/**"
    }
    from "LICENSE.md"
    manifest.mainAttributes(
            "Implementation-Title": project.archivesBaseName + "-api",
            "Implementation-Version": project.version,
            "Maven-Artifact": "${project.group}:${project.name}:${project.version}:api".toLowerCase(Locale.ROOT),
            "Built-On-Minecraft": "${mc_version}",
            "Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
    )
    classifier = "api"
}

task remapApiJar(type: RemapJar, dependsOn: apiJar) {
    jar = apiJar.archivePath
}

//shadowJar {
//    classifier = null
//    configurations = [project.configurations.embed]
//}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    from "LICENSE.md"
    classifier = "javadoc"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
            artifact(apiJar) {
                builtBy remapApiJar
            }
            artifact javadocJar
            pom {
                name = "Mesh"
                url = "https://github.com/GlassPane/Mesh"
                licenses {
                    license {
                        name = "GNU Lesser General Public License"
                        url = "https://github.com/GlassPane/Mesh/blob/master/LICENSE.md"
                    }
                }
                scm {
                    url = "https://github.com/GlassPane/Mesh"
                    connection = "scm:git:git://github.com/GlassPane/mesh.git"
                    developerConnection = "scm:git:git@github.com:GlassPane/Mesh.git"
                }
                issueManagement {
                    system = "github"
                    url = "https://github.com/GlassPane/Mesh/issues"
                }
            }
        }
    }

    repositories {
        //mavenLocal() //uncomment this to publish to the local maven

        //publish to the nerdhub maven if credentials are set
        if (project.hasProperty("nerdhub_maven_user")) {
            maven {
                name = "NerdHub"
                url = findProperty("nerdhub_maven_url")
                credentials {
                    username = findProperty("nerdhub_maven_user")
                    password = findProperty("nerdhub_maven_pass")
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}

license {
    header = file("code_quality/${project.license_header}_HEADER.txt")
    // Apply licenses only to main source set
    sourceSets = [project.sourceSets.main]
    include "**/*.java"
    charset = StandardCharsets.UTF_8.name()

    style {
        java = "BLOCK_COMMENT"
    }
    newLine = false // Disables the empty line between the header and package name
    //ignoreFailures = true //Ignore failures and only print a warning on license violations

    //export variables
    ext {
        year = Year.now()
        projectDisplayName = project.archivesBaseName
    }
}
tasks.build.dependsOn javadocJar, remapApiJar
